module Chapter4-Relations where 

open import Chapter1-Agda 
  using (Bool; false; true; not; _×_) 

open import Chapter2-Numbers 
  using (ℕ; zero; suc; _+_) 

open import Chapter3-Proofs 

open import Agda.Primitive 
  using (Level; _⊔_; lzero; lsuc) 

module Playground-Level where 
  data Maybe₀ (A : Set) : Set where 
    just₀ : A → Maybe₀ A 
    nothing₀ : Maybe₀ A 
    
  data Maybe₁ {ℓ : Level} (A : Set ℓ) : Set ℓ where 
    just₁ : A → Maybe₁ A 
    nothing₁ : Maybe₁ A 

  _ = just₁ ℕ 

  private variable 
    ℓ : Level 

  data Maybe₂ (A : Set ℓ) : Set ℓ where 
    just₂ : A → Maybe₂ A 
    nothing₂ : Maybe₂ A 

private variable 
  ℓ ℓ₁ ℓ₂ a b c : Level 
  A : Set a 
  B : Set b 
  C : Set c 

module Definition-DependentPair where 
  open Chapter3-Proofs 

  record Σ (A : Set ℓ₁) (B : A → Set ℓ₂) 
      : Set (lsuc (ℓ₁ ⊔ ℓ₂)) where 
    constructor _,_ 
    field 
      proj₁ : A 
      proj₂ : B proj₁ 

  {- record _×_ (A : Set ℓ₁) (B : Set ℓ₂) : Set (lsuc (ℓ₁ ⊔ ℓ₂)) where 
    constructor _,_ 
    field 
      proj₁ : A 
      proj₂ : B  -}

  -- this won't recognize 4 for some reason
  --∃n,n+1≡ : Σ ℕ (λ n → n + 1 ≡ 5) 
  --∃n,n+1≡ = 4, PropEq.refl 

open import Data.Product 
  using (Σ; _,_) 

module Sandbox-Relations where 
  REL : Set a → Set b → (ℓ : Level) 
      → Set (a ⊔ b ⊔ lsuc ℓ) 
  REL A B ℓ = A → B → Set ℓ 

  data Unrelated : REL A B lzero where 

  data Related (A : Set a) (B : Set b) : REL A B (a ⊔ b) where 
    related : {x : A} {y : B} → Related A B x y

  data Foo : Set where 
    f1 f2 f3 : Foo 

  data Bar : Set where 
    b1 b2 b3 : Bar 

  data FooBar : REL Foo Bar lzero where 
    f2-b2  : FooBar f2 b2 
    f2-b2′ : FooBar f2 b2 
    f2-b   : (b : Bar) → FooBar f2 b 
    f3-b2  : FooBar f3 b2 
  
  data _maps_↦_ {A : Set a} {B : Set b} (f : A → B) : REL A B a where
    app : {x : A} → f maps x ↦ f x

  _ : not maps false ↦ true 
  _ = app 

  _ : not maps true ↦ false 
  _ = app 

  Functional : REL A B ℓ → Set _ 
  Functional {A = A} {B = B} _~_ = {x : A} {y z : B} → x ~ y → x ~ z → y ≡ z

  Total : REL A B ℓ → Set _ 
  Total {A = A} {B = B} _~_ = (x : A) → Σ B (λ y → x ~ y) 

  relToFn : (_~_ : REL A B ℓ) → Functional _~_ → Total _~_ → A → B 
  relToFn _~_ _ total x with total x 
  ... | y , _ = y

  Rel : Set a → (ℓ : Level) → Set (a ⊔ lsuc ℓ) 
  Rel A ℓ = REL A A ℓ 

  Reflexive : Rel A ℓ → Set _ 
  Reflexive {A = A} _~_ = {x : A} → x ~ x 

  Symmetric : Rel A ℓ → Set _ 
  Symmetric {A = A} _~_ = {x y : A} → x ~ y → y ~ x

  Transitive : Rel A ℓ → Set _ 
  Transitive {A = A} _~_ = {x y z : A} → x ~ y → y ~ z → x ~ z 

open import Relation.Binary 
  using (Rel; Reflexive; Transitive; Symmetric) 

module Naive-≤₁ where 
  data _≤_ : Rel ℕ lzero where 
    lte : (a b : ℕ) → a ≤ a + b 
  infix 4 _≤_ 

  _ : 2 ≤ 5 
  _ = lte 2 3

  suc-mono : {x y : ℕ} → x ≤ y → suc x ≤ suc y 
  suc-mono (lte x y) = lte (suc x) y 

  ≤-refl : Reflexive _≤_ 
  ≤-refl {zero} = lte zero zero
  ≤-refl {suc x} with ≤-refl {x} 
  ... | x≤x = suc-mono x≤x

  open Chapter3-Proofs 
    using (+-identityʳ) 
  
  subst : {x y : A} → (P : A → Set ℓ) → x ≡ y → P x → P y 
  subst _ PropEq.refl px = px

  ≤-refl′ : Reflexive _≤_ 
  ≤-refl′ {x} = subst (λ y → x ≤ y) (+-identityʳ x) (lte x 0)

  suc-mono′ : {x y : ℕ} → x ≤ y → suc x ≤ suc y 
  suc-mono′ {x} {y} (lte a b) = lte (suc x) b

module Definition-LessThanOrEqualTo where 
  data _≤_ : Rel ℕ lzero where 
    z≤n : {n : ℕ} → zero ≤ n 
    s≤s : {m n : ℕ} → m ≤ n → suc m ≤ suc n 
  infix 4 _≤_ 

open import Data.Nat 
  using (_≤_; z≤n; s≤s) 

module Sandbox-≤ where 
  _ : 2 ≤ 5 
  _ = s≤s (s≤s z≤n)

  suc-mono : {x y : ℕ} → x ≤ y → suc x ≤ suc y 
  suc-mono = s≤s

  ≤-refl : {x : ℕ} → x ≤ x 
  ≤-refl {zero} = z≤n
  ≤-refl {suc x} = s≤s ≤-refl

  ≤-trans : {x y z : ℕ} → x ≤ y → y ≤ z → x ≤ z 
  ≤-trans z≤n h2 = z≤n
  ≤-trans (s≤s h1) (s≤s h2) = s≤s (≤-trans h1 h2)


module Sandbox-Preorders where 
  open Sandbox-≤ 

  record IsPreorder {A : Set a} (_~_ : Rel A ℓ) : Set (a ⊔ ℓ) where 
    field 
      refl  : Reflexive _~_ 
      trans : Transitive _~_ 
  
  ≤-preorder : IsPreorder _≤_ 
  IsPreorder.refl ≤-preorder = ≤-refl 
  IsPreorder.trans ≤-preorder = ≤-trans 

  ≡-preorder : IsPreorder (_≡_ {A = A}) 
  IsPreorder.refl ≡-preorder = PropEq.refl 
  IsPreorder.trans ≡-preorder = PropEq.trans 

  open Sandbox-Relations using (Related; related) 

  Related-preorder : IsPreorder (Related A A) 
  IsPreorder.refl Related-preorder = related 
  IsPreorder.trans Related-preorder _ _ = related

  module Preorder-Reasoning {_~_ : Rel A ℓ} (~-preorder : IsPreorder _~_) where 
    open IsPreorder ~-preorder public 
    
    --left off on page 189 Section 4.13









  